module interp_mod

  use const_mod
  use namelist_mod
  use block_mod
  use process_mod
  use parallel_mod
  use upwind_mod
  use weno_mod
  use time_mod
  use member_mod
  use pa_mod

  implicit none

  private

  !                              / lev_lat_edge
  !               o-------------o------------o lev_vtx
  !              /|            /            /|
  !             / |                        / |
  !            /  |        |              /  |
  !           o   |        o lev_edge   -o- lev_lon_edge
  !          /    |        |            /    |
  !         /     o vtx                /     o vtx
  !        /      |                   /      |
  !       o-------+-----o------------o       |
  !       |       |                  |       |
  ! lon_edge -o-  |        o cell    |  -o- lon_edge
  !       |       |                  |       |
  !       |       o------------------+-------o
  !       |      /       /           |      /
  !       o vtx /       o lat_edge   o vtx /
  !       |    /       /             |    /
  !       |   o                      |   o
  !       |  /                       |  /
  !       | /                        | /
  !       |/                         |/
  !       o-------------o------------o
  !
  public interp_init
  public interp_final
  public interp_cell_to_lon_edge
  public interp_cell_to_lat_edge
  public interp_cell_to_lev_edge
  public average_cell_to_lon_edge
  public average_cell_to_lat_edge
  public interp_cell_to_vtx
  public interp_lon_edge_to_cell
  public interp_lat_edge_to_cell
  public interp_lon_edge_to_lev_lon_edge
  public interp_lat_edge_to_lev_lat_edge
  public interp_lev_edge_to_cell
  public interp_lev_edge_to_lev_lon_edge
  public interp_lev_edge_to_lev_lat_edge
  public interp_cell_to_height_level
  public interp_lon_edge_to_height_level
  public interp_lat_edge_to_height_level
  public interp_lev_edge_to_height_level
  public interp_cell_to_pressure_level
  public interp_lon_edge_to_pressure_level
  public interp_lat_edge_to_pressure_level
  public interp_lev_edge_to_pressure_level

contains

  subroutine interp_init()

    call interp_final()

  end subroutine interp_init

  subroutine interp_final()

  end subroutine interp_final

  subroutine interp_cell_to_lon_edge(mesh, x, x_lon, reversed_area, u, upwind_wgt_)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x(member_num ,                       &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(inout) :: x_lon(member_num ,                       &
                                     mesh%half_ims:mesh%half_ime, &
                                     mesh%full_jms:mesh%full_jme, &
                                     mesh%full_kms:mesh%full_kme)
    logical, intent(in), optional :: reversed_area
    real(r8), intent(in), optional :: u(member_num ,                       &
                                        mesh%half_ims:mesh%half_ime, &
                                        mesh%full_jms:mesh%full_jme, &
                                        mesh%full_kms:mesh%full_kme)

    real(r8), intent(in), optional :: upwind_wgt_

    real(r8)  beta(mesh%full_jds:mesh%full_jde)
    integer i, j, k, im


#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_cell_to_lon_edge")
    call Indent_In()
#endif
    if (present(u)) then
      ! WENO interpolation
      select case (weno_order)
      case (3)
        do k = mesh%full_kds, mesh%full_kde
          do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
            do i = mesh%half_ids, mesh%half_ide
              do im = 1 , member_num
                x_lon(im,i,j,k) = weno3(sign(1.0_r8, u(im,i,j,k)), x(im,i-1:i+2,j,k))
              end do
            end do
          end do
        end do
        return
      end select
      ! Upwind-biased interpolation
      beta = merge(upwind_wgt_, upwind_wgt, present(upwind_wgt_))
      select case (upwind_order)
      case (1)
        do k = mesh%full_kds, mesh%full_kde
          do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
            do i = mesh%half_ids, mesh%half_ide
              do im = 1 , member_num
                x_lon(im,i,j,k) = upwind1(sign(1.0_r8, u(im,i,j,k)), beta(j), x(im,i:i+1,j,k))
              end do
            end do
          end do
        end do
        return
      case(3)
        do k = mesh%full_kds, mesh%full_kde
          do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
            do i = mesh%half_ids, mesh%half_ide
              do im = 1 , member_num
                x_lon(im,i,j,k) = upwind3(sign(1.0_r8, u(im,i,j,k)), beta(j), x(im,i-1:i+2,j,k))
              end do
            end do
          end do
        end do
        return
      end select
    end if

    if (merge(reversed_area, .false., present(reversed_area))) then
      do k = mesh%full_kds, mesh%full_kde
        do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
          do i = mesh%half_ids, mesh%half_ide
            x_lon(:,i,j,k) = (mesh%area_lon_east(j) * x(:,i  ,j,k) + &
                              mesh%area_lon_west(j) * x(:,i+1,j,k)   &
                             ) / mesh%area_lon(j)
          end do
        end do
      end do
    else ! reversed_area == .false.
      do k = mesh%full_kds, mesh%full_kde
        do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
          do i = mesh%half_ids, mesh%half_ide
            x_lon(:,i,j,k) = (mesh%area_lon_west(j) * x(:,i  ,j,k) + &
                              mesh%area_lon_east(j) * x(:,i+1,j,k)   &
                             ) / mesh%area_lon(j)
          end do
        end do
      end do
    end if
#ifdef Detail_Time  
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif


  end subroutine interp_cell_to_lon_edge

  subroutine interp_cell_to_lat_edge(mesh, x, x_lat, reversed_area, v, upwind_wgt_)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x(member_num ,                       & 
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(inout) :: x_lat(member_num ,                       &
                                     mesh%full_ims:mesh%full_ime, &
                                     mesh%half_jms:mesh%half_jme, &
                                     mesh%full_kms:mesh%full_kme)
    logical, intent(in), optional :: reversed_area
    real(r8), intent(in), optional :: v(member_num ,                       &
                                        mesh%full_ims:mesh%full_ime, &
                                        mesh%half_jms:mesh%half_jme, &
                                        mesh%full_kms:mesh%full_kme)
    real(r8), intent(in), optional :: upwind_wgt_

    real(r8)  beta(mesh%full_jds:mesh%full_jde)
    integer i, j, k, im


#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_cell_to_lat_edge")
    call Indent_In()
#endif
    if (present(v)) then
      beta = merge(upwind_wgt_, upwind_wgt, present(upwind_wgt_))
      ! WENO interpolation
      select case (weno_order)
      case (3)
         do k = mesh%full_kds, mesh%full_kde
          do j = mesh%half_jds, mesh%half_jde
            do i = mesh%full_ids, mesh%full_ide
              do im = 1 , member_num
                x_lat(im,i,j,k) = weno3(sign(1.0_r8, v(im,i,j,k)), x(im,i,j-1:j+2,k))
              end do
            end do
           end do
        end do
        return
      end select
      ! Upwind-biased interpolation
      select case (upwind_order)
      case (1)
        do k = mesh%full_kds, mesh%full_kde
          do j = mesh%half_jds, mesh%half_jde
            do i = mesh%full_ids, mesh%full_ide
              do im = 1 , member_num
                x_lat(im,i,j,k) = upwind1(sign(1.0_r8, v(im,i,j,k)), beta(j), x(im,i,j:j+1,k))
              end do
            end do
          end do
        end do
        return
      case(3)
        do k = mesh%full_kds, mesh%full_kde
          do j = mesh%half_jds, mesh%half_jde
              do i = mesh%full_ids, mesh%full_ide
                do im = 1 , member_num
                  x_lat(im,i,j,k) = upwind3(sign(1.0_r8, v(im,i,j,k)), beta(j), x(im,i,j-1:j+2,k))
                end do
              end do
          end do
        end do
        return
      end select
    end if

    if (merge(reversed_area, .false., present(reversed_area))) then
      do k = mesh%full_kds, mesh%full_kde
        do j = mesh%half_jds, mesh%half_jde
          do i = mesh%full_ids, mesh%full_ide
            x_lat(:,i,j,k) = (mesh%area_lat_south(j) * x(:,i,j+1,k) + &
                              mesh%area_lat_north(j) * x(:,i,j  ,k)   &
                             ) / mesh%area_lat(j)
          end do
        end do
      end do
    else ! reversed_area == .false.
      do k = mesh%full_kds, mesh%full_kde
        do j = mesh%half_jds, mesh%half_jde
          do i = mesh%full_ids, mesh%full_ide
            x_lat(:,i,j,k) = (mesh%area_lat_north(j) * x(:,i,j+1,k) + &
                              mesh%area_lat_south(j) * x(:,i,j  ,k)   &
                             ) / mesh%area_lat(j)
          end do
        end do
      end do
    endif
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif
    

  end subroutine interp_cell_to_lat_edge

  subroutine average_cell_to_lon_edge(mesh, x, x_lon)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in ) :: x    (member_num ,&
                                   mesh%full_ims:mesh%full_ime, &
                                   mesh%full_jms:mesh%full_jme, &
                                   mesh%full_kms:mesh%full_kme)
    real(r8), intent(out) :: x_lon(member_num ,&
                                   mesh%half_ims:mesh%half_ime, &
                                   mesh%full_jms:mesh%full_jme, &
                                   mesh%full_kms:mesh%full_kme)

    integer i, j, k

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("average_cell_to_lon_edge")
    call Indent_In()
#endif
    do k = mesh%full_kds, mesh%full_kde
      do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
        do i = mesh%half_ids, mesh%half_ide
          x_lon(:,i,j,k) = (x(:,i,j,k) + x(:,i+1,j,k)) * 0.5_r8
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine average_cell_to_lon_edge

  subroutine average_cell_to_lat_edge(mesh, x, x_lat)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in ) :: x    (member_num ,&
                                   mesh%full_ims:mesh%full_ime, &
                                   mesh%full_jms:mesh%full_jme, &
                                   mesh%full_kms:mesh%full_kme)
    real(r8), intent(out) :: x_lat(member_num ,&
                                   mesh%full_ims:mesh%full_ime, &
                                   mesh%half_jms:mesh%half_jme, &
                                   mesh%full_kms:mesh%full_kme)

    integer i, j, k

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("average_cell_to_lat_edge")
    call Indent_In()
#endif
    do k = mesh%full_kds, mesh%full_kde
      do j = mesh%half_jds, mesh%half_jde
        do i = mesh%full_ids, mesh%full_ide
          x_lat(:,i,j,k) = (x(:,i,j,k) + x(:,i,j+1,k)) * 0.5_r8
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine average_cell_to_lat_edge

  subroutine interp_lev_edge_to_cell(mesh, x_lev, x)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x_lev(member_num ,                       &
                                  mesh%full_ims:mesh%full_ime, &
                                  mesh%full_jms:mesh%full_jme, &
                                  mesh%half_kms:mesh%half_kme)
    real(r8), intent(inout) :: x(member_num ,                       &
                                 mesh%full_ims:mesh%full_ime, &
                                 mesh%full_jms:mesh%full_jme, &
                                 mesh%full_kms:mesh%full_kme)  
    
    integer i, j, k, im


#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lev_edge_to_cell")
    call Indent_In()
#endif
    do k = mesh%full_kds, mesh%full_kde
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          x(:,i,j,k) = 0.5_r8 * (x_lev(:,i,j,k) + x_lev(:,i,j,k+1))
        end do
      end do
    end do

#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lev_edge_to_cell


  subroutine interp_lev_edge_to_lev_lon_edge(mesh, x_lev, x_lev_lon, u, upwind_wgt_)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x_lev(member_num ,                       &
                                  mesh%full_ims:mesh%full_ime, &
                                  mesh%full_jms:mesh%full_jme, &
                                  mesh%half_kms:mesh%half_kme)
    real(r8), intent(inout) :: x_lev_lon(member_num ,                       &
                                         mesh%half_ims:mesh%half_ime, &
                                         mesh%full_jms:mesh%full_jme, &
                                         mesh%half_kms:mesh%half_kme)
    real(r8), intent(in), optional :: u(member_num ,                       &
                                        mesh%half_ims:mesh%half_ime, &
                                        mesh%full_jms:mesh%full_jme, &
                                        mesh%half_kms:mesh%half_kme)
     real(r8), intent(in), optional :: upwind_wgt_
    
     real(r8)  beta
     integer i, j, k, im


#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lev_edge_to_lev_lon_edge")
    call Indent_In()
#endif
    if (present(u)) then
      ! WENO interpolation
      select case (weno_order)
      case (3)
        do k = mesh%half_kds, mesh%half_kde
          do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
            do i = mesh%half_ids, mesh%half_ide
              do im = 1 , member_num
                x_lev_lon(im,i,j,k) = weno3(sign(1.0_r8, u(im,i,j,k)), x_lev(im,i-1:i+2,j,k))
              end do
            end do
          end do
        end do
        return
      end select
      ! Upwind-biased interpolation
      if (present(upwind_wgt_)) then
        beta = upwind_wgt_
      else
        beta = upwind_wgt
      end if
      select case (upwind_order)
      case (1)
        do k = mesh%half_kds, mesh%half_kde
          do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
            do i = mesh%half_ids, mesh%half_ide
              do im = 1 , member_num
                x_lev_lon(im,i,j,k) = upwind1(sign(1.0_r8, u(im,i,j,k)), beta, x_lev(im,i:i+1,j,k))
              end do
            end do
          end do
        end do
        return
      case (3)
        do k = mesh%half_kds, mesh%half_kde
          do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
            do i = mesh%half_ids, mesh%half_ide
              do im = 1 , member_num
                x_lev_lon(im,i,j,k) = upwind3(sign(1.0_r8, u(im,i,j,k)), beta, x_lev(im,i-1:i+2,j,k))
              end do
            end do
          end do
        end do
        return
      end select
    end if 

    do k = mesh%half_kds, mesh%half_kde
      do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
        do i = mesh%half_ids, mesh%half_ide
          x_lev_lon(:,i,j,k) = (mesh%area_lon_west(j) * x_lev(:,i  ,j,k) + &
                                mesh%area_lon_east(j) * x_lev(:,i+1,j,k)   &
                               ) / mesh%area_lon(j)
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif


  end subroutine interp_lev_edge_to_lev_lon_edge

  subroutine interp_lev_edge_to_lev_lat_edge(mesh, x_lev, x_lev_lat, v, upwind_wgt_,debug)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x_lev(member_num ,                       &
                                  mesh%full_ims:mesh%full_ime, &
                                  mesh%full_jms:mesh%full_jme, &
                                  mesh%half_kms:mesh%half_kme)
    real(r8), intent(inout) :: x_lev_lat(member_num,                        &
                                         mesh%full_ims:mesh%full_ime, &
                                         mesh%half_jms:mesh%half_jme, &
                                         mesh%half_kms:mesh%half_kme)
    real(r8), intent(in), optional :: v(member_num ,                       &
                                        mesh%full_ims:mesh%full_ime, &
                                        mesh%half_jms:mesh%half_jme, &
                                        mesh%half_kms:mesh%half_kme)
    real(r8), intent(in), optional :: upwind_wgt_
    logical , intent(in), optional :: debug
    
    real(r8)  beta
    integer i, j, k, im


#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lev_edge_to_lev_lat_edge")
    call Indent_In()
#endif
    if (present(v)) then
      if (present(upwind_wgt_)) then
        beta = upwind_wgt_
      else
        beta = upwind_wgt
      end if
      ! WENO interpolation
      select case (weno_order)
      case (3)
        do k = mesh%half_kds, mesh%half_kde
          do j = mesh%half_jds, mesh%half_jde
            do i = mesh%full_ids, mesh%full_ide
              do im = 1 , member_num
                x_lev_lat(im,i,j,k) = weno3(sign(1.0_r8, v(im,i,j,k)), x_lev(im,i,j-1:j+2,k))
              end do
            end do
          end do
        end do
        return
      end select
      ! Upwind-biased interpolation
      select case (upwind_order)
      case (1)
        do k = mesh%half_kds, mesh%half_kde
          do j = mesh%half_jds, mesh%half_jde
            do i = mesh%full_ids, mesh%full_ide
              do im = 1 , member_num
                x_lev_lat(im,i,j,k) = upwind1(sign(1.0_r8, v(im,i,j,k)), beta, x_lev(im,i,j:j+1,k))
              end do
            end do
          end do
        end do
        return
      case (3)
        do k = mesh%half_kds, mesh%half_kde
          do j = mesh%half_jds, mesh%half_jde
            do i = mesh%full_ids, mesh%full_ide
              do im = 1 , member_num
                x_lev_lat(im,i,j,k) = upwind3(sign(1.0_r8, v(im,i,j,k)), beta, x_lev(im,i,j-1:j+2,k))
              end do
            end do
          end do
        end do
        return
      end select
    end if

    do k = mesh%half_kds, mesh%half_kde
      do j = mesh%half_jds, mesh%half_jde
        do i = mesh%full_ids, mesh%full_ide
          x_lev_lat(:,i,j,k) = (mesh%area_lat_north(j) * x_lev(:,i,j+1,k) + &
                                mesh%area_lat_south(j) * x_lev(:,i,j  ,k)   &
                               ) / mesh%area_lat(j)
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif


  end subroutine interp_lev_edge_to_lev_lat_edge

  subroutine interp_cell_to_vtx(mesh, x, x_vtx)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x(member_num ,                       &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(inout) :: x_vtx(member_num ,                       &
                                     mesh%half_ims:mesh%half_ime, &
                                     mesh%half_jms:mesh%half_jme, &
                                     mesh%full_kms:mesh%full_kme)

    integer i, j, k

 

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_cell_to_vtx")
    call Indent_In()
#endif
    do k = mesh%full_kds, mesh%full_kde
      do j = mesh%half_jds, mesh%half_jde
        do i = mesh%half_ids, mesh%half_ide
          x_vtx(:,i,j,k) = (                                           &
            (x(:,i,j  ,k) + x(:,i+1,j  ,k)) * mesh%area_subcell(2,j  ) + &
            (x(:,i,j+1,k) + x(:,i+1,j+1,k)) * mesh%area_subcell(1,j+1)   &
          ) / mesh%area_vtx(j)
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif


  end subroutine interp_cell_to_vtx

  subroutine interp_cell_to_lev_edge(mesh, x, x_lev)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x(member_num ,                       &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(inout) :: x_lev(member_num ,                       &
                                     mesh%full_ims:mesh%full_ime, &
                                     mesh%full_jms:mesh%full_jme, &
                                     mesh%half_kms:mesh%half_kme)
    
    integer i, j, k, im
    real(r8) x1, x2, a, b, beta

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_cell_to_lev_edge")
    call Indent_In()
#endif

    if (mesh%full_nlev == 1) return

    ! --------------------------------------------------------------------------
    ! -------
    !
    ! ===o=== k-1
    !
    ! ---?--- k
    !
    ! ===o=== k
    !
    ! -------
    do k = mesh%half_kds + 1, mesh%half_kde - 1
      a = mesh%full_dlev(k-1) / (2 * mesh%half_dlev(k))
      b = mesh%full_dlev(k  ) / (2 * mesh%half_dlev(k))
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          x_lev(:,i,j,k) = a * x(:,i,j,k-1) + b * x(:,i,j,k)
        end do
      end do
    end do

    k = mesh%half_kds
    x1 = mesh%full_lev(k  ) - mesh%half_lev(k)
    x2 = mesh%full_lev(k+1) - mesh%half_lev(k)
    a =  x2 / (x2 - x1)
    b = -x1 / (x2 - x1)
    do j = mesh%full_jds, mesh%full_jde
      do i = mesh%full_ids, mesh%full_ide
        x_lev(:,i,j,k) = a * x(:,i,j,k) + b * x(:,i,j,k+1)
      end do
    end do
    k = mesh%half_kde
    x1 = mesh%half_lev(k) - mesh%full_lev(k-1)
    x2 = mesh%half_lev(k) - mesh%full_lev(k-2)
    a =  x2 / (x2 - x1)
    b = -x1 / (x2 - x1)
    do j = mesh%full_jds, mesh%full_jde
      do i = mesh%full_ids, mesh%full_ide
        x_lev(:,i,j,k) = a * x(:,i,j,k-1) + b * x(:,i,j,k-2)
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_cell_to_lev_edge

  subroutine interp_cell_to_height_level(mesh, z, x, zo, y)  

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: z(member_num                       , &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: x(member_num                       , &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: zo
    real(r8), intent(inout) :: y(member_num                       , &
                                 mesh%full_ims:mesh%full_ime, &
                                 mesh%full_jms:mesh%full_jme)

    real(r8) dz1, dz2, z1, z2, a, b
    integer i, j, k, im

    ! --o-- z(k-1)
    !
    ! --?-- zo
    !
    ! --o-- z(k)
#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_cell_to_height_level")
    call Indent_In()
#endif
    do im = 1 , member_num
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          do k = mesh%full_kde, mesh%full_kds + 1, -1
            if (z(im,i,j,k) <= zo .and. zo <= z(im,i,j,k-1)) then
              dz1 = z(im,i,j,k-1) - zo
              dz2 = zo - z(im,i,j,k)
              y(im,i,j) = (dz2 * x(im,i,j,k-1) + dz1 * x(im,i,j,k)) / (dz1 + dz2)
              exit
            else if (zo < z(im,i,j,k) .and. k == mesh%full_kde) then
              z1 = z(im,i,j,k  ) - zo
              z2 = z(im,i,j,k-1) - zo
              a  =  z2 / (z2 - z1)
              b  = -z1 / (z2 - z1)
              y(im,i,j) = a * x(im,i,j,k) + b * x(im,i,j,k-1)
              exit
            else if (zo > z(im,i,j,k-1) .and. k == mesh%full_kds + 1) then
              z1 = zo - z(im,i,j,k-1)
              z2 = zo - z(im,i,j,k  )
              a  =  z2 / (z2 - z1)
              b  = -z1 / (z2 - z1)
              y(im,i,j) = a * x(im,i,j,k-1) + b * x(im,i,j,k)
              exit
            end if
          end do
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_cell_to_height_level

  subroutine interp_lon_edge_to_height_level(mesh, z, x, zo, y)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: z(member_num                       , &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: x(member_num                       , &
                              mesh%half_ims:mesh%half_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: zo
    real(r8), intent(out) :: y(member_num                       , &
                               mesh%full_ims:mesh%full_ime, &
                               mesh%full_jms:mesh%full_jme)

    real(r8) dz1, dz2, x1, x2, z1, z2, a, b
    integer i, j, k, im

    !               x1
    ! x(i-1,k-1) o--x--o x(i,k-1)
    !
    !               ? zo
    !
    ! x(i-1,k  ) o--x--o x(i,k  )
    !               x2
#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lon_edge_to_height_level")
    call Indent_In()
#endif
    do im = 1 , member_num
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          do k = mesh%full_kde, mesh%full_kds + 1, -1
            if (z(im,i,j,k) <= zo .and. zo <= z(im,i,j,k-1)) then
              dz1 = z(im,i,j,k-1) - zo
              dz2 = zo - z(im,i,j,k)
              x1 = 0.5_r8 * (x(im,i-1,j,k-1) + x(im,i,j,k-1))
              x2 = 0.5_r8 * (x(im,i-1,j,k  ) + x(im,i,j,k  ))
              y(im,i,j) = (dz2 * x1 + dz1 * x2) / (dz1 + dz2)
              exit
            else if (zo < z(im,i,j,k) .and. k == mesh%full_kde) then
              z1 = z(im,i,j,k  ) - zo
              z2 = z(im,i,j,k-1) - zo
              a  =  z2 / (z2 - z1)
              b  = -z1 / (z2 - z1)
              x1 = 0.5_r8 * (x(im,i-1,j,k-1) + x(im,i,j,k-1))
              x2 = 0.5_r8 * (x(im,i-1,j,k  ) + x(im,i,j,k  ))
              y(im,i,j) = a * x1 + b * x2
              exit
            else if (zo > z(im,i,j,k-1) .and. k == mesh%full_kds + 1) then
              z1 = zo - z(im,i,j,k-1)
              z2 = zo - z(im,i,j,k  )
              a  =  z2 / (z2 - z1)
              b  = -z1 / (z2 - z1)
              x1 = 0.5_r8 * (x(im,i-1,j,k-1) + x(im,i,j,k-1))
              x2 = 0.5_r8 * (x(im,i-1,j,k  ) + x(im,i,j,k  ))
              y(im,i,j) = a * x1 + b * x2
              exit
            end if
          end do
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lon_edge_to_height_level

  subroutine interp_lat_edge_to_height_level(mesh, z, x, zo, y)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: z(member_num                       , &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: x(member_num                       , &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%half_jms:mesh%half_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: zo
    real(r8), intent(out) :: y(member_num                       , &
                               mesh%full_ims:mesh%full_ime, &
                               mesh%full_jms:mesh%full_jme)

    real(r8) dz1, dz2, x1, x2, z1, z2, a, b
    integer i, j, k, im

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lat_edge_to_height_level")
    call Indent_In()
#endif
    do im = 1 , member_num
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          do k = mesh%full_kde, mesh%full_kds + 1, -1
            if (z(im,i,j,k) <= zo .and. zo <= z(im,i,j,k-1)) then
              dz1 = z(im,i,j,k-1) - zo
              dz2 = zo - z(im,i,j,k)
              x1 = 0.5_r8 * (x(im,i,j-1,k-1) + x(im,i,j,k-1))
              x2 = 0.5_r8 * (x(im,i,j-1,k  ) + x(im,i,j,k  ))
              y(im,i,j) = (dz2 * x1 + dz1 * x2) / (dz1 + dz2)
              exit
            else if (zo < z(im,i,j,k) .and. k == mesh%full_kde) then
              z1 = z(im,i,j,k  ) - zo
              z2 = z(im,i,j,k-1) - zo
              a  =  z2 / (z2 - z1)
              b  = -z1 / (z2 - z1)
              x1 = 0.5_r8 * (x(im,i,j-1,k-1) + x(im,i,j,k-1))
              x2 = 0.5_r8 * (x(im,i,j-1,k  ) + x(im,i,j,k  ))
              y(im,i,j) = a * x1 + b * x2
              exit
            else if (zo > z(im,i,j,k-1) .and. k == mesh%full_kds + 1) then
              z1 = zo - z(im,i,j,k-1)
              z2 = zo - z(im,i,j,k  )
              a  =  z2 / (z2 - z1)
              b  = -z1 / (z2 - z1)
              x1 = 0.5_r8 * (x(im,i,j-1,k-1) + x(im,i,j,k-1))
              x2 = 0.5_r8 * (x(im,i,j-1,k  ) + x(im,i,j,k  ))
              y(im,i,j) = a * x1 + b * x2
              exit
            end if
          end do
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lat_edge_to_height_level

  subroutine interp_lev_edge_to_height_level(mesh, z, x, zo, y)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: z(member_num                       , &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%half_kms:mesh%half_kme)
    real(r8), intent(in) :: x(member_num                       , &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%half_kms:mesh%half_kme)
    real(r8), intent(in) :: zo
    real(r8), intent(out) :: y(member_num                       , &
                               mesh%full_ims:mesh%full_ime, &
                               mesh%full_jms:mesh%full_jme)

    real(r8) dz1, dz2, x1, x2, z1, z2, a, b
    integer i, j, k, im

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lev_edge_to_height_level")
    call Indent_In()
#endif
    do im = 1 , member_num
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          do k = mesh%half_kde, mesh%half_kds + 1, -1
            if (z(im,i,j,k) <= zo .and. zo <= z(im,i,j,k-1)) then
              dz1 = z(im,i,j,k-1) - zo
              dz2 = zo - z(im,i,j,k)
              y(im,i,j) = (dz2 * x(im,i,j,k-1) + dz1 * x(im,i,j,k)) / (dz1 + dz2)
              exit
            else if (zo < z(im,i,j,k) .and. k == mesh%full_kde) then
              z1 = z(im,i,j,k  ) - zo
              z2 = z(im,i,j,k-1) - zo
              a  =  z2 / (z2 - z1)
              b  = -z1 / (z2 - z1)
              y(im,i,j) = a * x(im,i,j,k) + b * x(im,i,j,k-1)
              exit
            else if (zo > z(im,i,j,k-1) .and. k == mesh%full_kds + 1) then
              z1 = zo - z(im,i,j,k-1)
              z2 = zo - z(im,i,j,k  )
              a  =  z2 / (z2 - z1)
              b  = -z1 / (z2 - z1)
              y(im,i,j) = a * x(im,i,j,k-1) + b * x(im,i,j,k)
              exit
            end if
          end do
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lev_edge_to_height_level


  subroutine interp_cell_to_pressure_level(mesh, p, x, po, y, logp)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: p(member_num ,                       & 
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: x(member_num ,                       &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: po
    real(r8), intent(inout) :: y(member_num ,                       &
                                 mesh%full_ims:mesh%full_ime, &
                                 mesh%full_jms:mesh%full_jme)
    logical, intent(in), optional :: logp

    logical logp_
    real(r8) p0, dp1, dp2
    integer i, j, k , im

    logp_ = merge(logp, .false., present(logp))

    p0 = merge(log(po), po, logp_)

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_cell_to_pressure_level")
    call Indent_In()
#endif
    do im = 1 , member_num
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          do k = mesh%full_kde, mesh%full_kds + 1, -1
            if (p(im,i,j,k-1) <= po .and. po <= p(im,i,j,k)) then
              if (logp_) then
                dp1 = p0 - log(p(im,i,j,k-1))
                dp2 = log(p(im,i,j,k)) - p0
              else
                dp1 = p0 - p(im,i,j,k-1)
                dp2 = p(im,i,j,k) - p0
              end if
              y(im,i,j) = (dp2 * x(im,i,j,k-1) + dp1 * x(im,i,j,k)) / (dp1 + dp2)
              exit
            end if
          end do
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_cell_to_pressure_level

  subroutine interp_lon_edge_to_pressure_level(mesh, p, x, po, y, logp)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: p(member_num ,                       &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: x(member_num ,                       &
                              mesh%half_ims:mesh%half_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: po
    real(r8), intent(out) :: y(member_num ,                       &
                               mesh%full_ims:mesh%full_ime, &
                               mesh%full_jms:mesh%full_jme)
    logical, intent(in), optional :: logp

    logical logp_
    real(r8) p0, dp1, dp2, x1, x2
    integer i, j, k , im

    logp_ = merge(logp, .false., present(logp))

    p0 = merge(log(po), po, logp_)

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lon_edge_to_pressure_level")
    call Indent_In()
#endif
    do im = 1 , member_num
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          do k = mesh%full_kde, mesh%full_kds + 1, -1
            if (p(im,i,j,k-1) <= po .and. po <= p(im,i,j,k)) then
              if (logp_) then
                dp1 = p0 - log(p(im,i,j,k-1))
                dp2 = log(p(im,i,j,k)) - p0
              else
                dp1 = p0 - p(im,i,j,k-1)
                dp2 = p(im,i,j,k) - p0
              end if
              x1 = 0.5_r8 * (x(im,i-1,j,k-1) + x(im,i,j,k-1))
              x2 = 0.5_r8 * (x(im,i-1,j,k  ) + x(im,i,j,k  ))
              y(im,i,j) = (dp2 * x1 + dp1 * x2) / (dp1 + dp2)
              exit
            end if
          end do
        end do
      end do
    end do  
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lon_edge_to_pressure_level

  subroutine interp_lat_edge_to_pressure_level(mesh, p, x, po, y, logp)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: p(member_num ,                       &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: x(member_num ,                       &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%half_jms:mesh%half_jme, &
                              mesh%full_kms:mesh%full_kme)
    real(r8), intent(in) :: po
    real(r8), intent(out) :: y(member_num ,                       &
                                 mesh%full_ims:mesh%full_ime, &
                                 mesh%full_jms:mesh%full_jme)
    logical, intent(in), optional :: logp

    logical logp_
    real(r8) p0,dp1, dp2, x1, x2
    integer i, j, k , im

    logp_ = merge(logp, .false., present(logp))

    p0 = merge(log(po), po, logp_)

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lat_edge_to_pressure_level")
    call Indent_In()
#endif
    do im = 1 , member_num
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          do k = mesh%full_kde, mesh%full_kds + 1, -1
            if (p(im,i,j,k-1) <= po .and. po <= p(im,i,j,k)) then
              if (logp_) then
                dp1 = p0 - log(p(im,i,j,k-1))
                dp2 = log(p(im,i,j,k)) - p0
              else
                dp1 = p0 - p(im,i,j,k-1)
                dp2 = p(im,i,j,k) - p0
              end if
              x1 = 0.5_r8 * (x(im,i,j-1,k-1) + x(im,i,j,k-1))
              x2 = 0.5_r8 * (x(im,i,j-1,k  ) + x(im,i,j,k  ))
              y(im,i,j) = (dp2 * x1 + dp1 * x2) / (dp1 + dp2)
              exit
            end if
          end do
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lat_edge_to_pressure_level

  subroutine interp_lev_edge_to_pressure_level(mesh, p, x, po, y, logp)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: p(member_num                       , &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%half_kms:mesh%half_kme)
    real(r8), intent(in) :: x(member_num                       , &
                              mesh%full_ims:mesh%full_ime, &
                              mesh%full_jms:mesh%full_jme, &
                              mesh%half_kms:mesh%half_kme)
    real(r8), intent(in) :: po
    real(r8), intent(out) :: y(member_num                       , &
                               mesh%full_ims:mesh%full_ime, &
                               mesh%full_jms:mesh%full_jme)
    logical, intent(in), optional :: logp

    logical logp_
    real(r8) p0, dp1, dp2, x1, x2
    integer i, j, k, im

    logp_ = merge(logp, .false., present(logp))

    p0 = merge(log(po), po, logp_)

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lev_edge_to_pressure_level")
    call Indent_In()
#endif
    do im = 1 , member_num
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%full_ids, mesh%full_ide
          do k = mesh%half_kde, mesh%half_kds + 1, -1
            if (p(im,i,j,k-1) <= po .and. po <= p(im,i,j,k)) then
              if (logp_) then
                dp1 = p0 - log(p(im,i,j,k-1))
                dp2 = log(p(im,i,j,k)) - p0
              else
                dp1 = p0 - p(im,i,j,k-1)
                dp2 = p(im,i,j,k) - p0
              end if
              y(im,i,j) = (dp2 * x(im,i,j,k-1) + dp1 * x(im,i,j,k)) / (dp1 + dp2)
              exit
            end if
          end do
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lev_edge_to_pressure_level

  subroutine interp_lon_edge_to_cell(mesh, x_lon, x)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x_lon(member_num ,                       & 
                                  mesh%half_ims:mesh%half_ime, &
                                  mesh%full_jms:mesh%full_jme, &
                                  mesh%full_kms:mesh%full_kme)
    real(r8), intent(out) :: x(member_num ,                       &
                               mesh%full_ims:mesh%full_ime, &
                               mesh%full_jms:mesh%full_jme, &
                               mesh%full_kms:mesh%full_kme)

    integer i, j, k

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lon_edge_to_cell")
    call Indent_In()
#endif
    do k = mesh%full_kds, mesh%full_kde
      do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
        do i = mesh%full_ids, mesh%full_ide
          x(:,i,j,k) = (mesh%area_lon_east(j) * x_lon(:,i-1,j,k) + &
                        mesh%area_lon_west(j) * x_lon(:,i  ,j,k)   &
                       ) / mesh%area_lon(j)
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lon_edge_to_cell

  subroutine interp_lat_edge_to_cell(mesh, x_lat, x)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x_lat(member_num ,                       &
                                  mesh%full_ims:mesh%full_ime, &
                                  mesh%half_jms:mesh%half_jme, &
                                  mesh%full_kms:mesh%full_kme)
    real(r8), intent(out) :: x(member_num ,                       &
                               mesh%full_ims:mesh%full_ime, &
                               mesh%full_jms:mesh%full_jme, &
                               mesh%full_kms:mesh%full_kme)

    integer i, j, k

#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lat_edge_to_cell")
    call Indent_In()
#endif
    do k = mesh%full_kds, mesh%full_kde
      do j = mesh%full_jds_no_pole, mesh%full_jde_no_pole
        do i = mesh%full_ids, mesh%full_ide
          x(:,i,j,k) = (mesh%area_lat_south(j  ) * x_lat(:,i,j  ,k) + &
                        mesh%area_lat_north(j-1) * x_lat(:,i,j-1,k)   &
                       ) / (mesh%area_lat_south(j) + mesh%area_lat_north(j-1))
        end do
      end do
    end do
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lat_edge_to_cell

  subroutine interp_lon_edge_to_lev_lon_edge(mesh, x_lon, x_lev_lon, handle_top_bottom)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x_lon(member_num ,                       &
                                  mesh%half_ims:mesh%half_ime, &
                                  mesh%full_jms:mesh%full_jme, &
                                  mesh%full_kms:mesh%full_kme)
    real(r8), intent(out) :: x_lev_lon(member_num  ,                      &
                                       mesh%half_ims:mesh%half_ime, &
                                       mesh%full_jms:mesh%full_jme, &
                                       mesh%half_kms:mesh%half_kme)
    logical, intent(in), optional :: handle_top_bottom

    integer i, j, k
    real(r8) x1, x2, x3, a, b, c

    ! -------
    !
    ! ===o=== k-1
    !
    ! ---?--- k
    !
    ! ===o=== k
    !
    ! ----o--
#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lon_edge_to_lev_lon_edge")
    call Indent_In()
#endif
    do k = mesh%half_kds + 1, mesh%half_kde - 1
      a = mesh%full_dlev(k-1) / (mesh%full_dlev(k-1) + mesh%full_dlev(k))
      b = mesh%full_dlev(k  ) / (mesh%full_dlev(k-1) + mesh%full_dlev(k))
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%half_ids, mesh%half_ide
          x_lev_lon(:,i,j,k) = a * x_lon(:,i,j,k) + b * x_lon(:,i,j,k-1)
        end do
      end do
    end do

    if (merge(handle_top_bottom, .false., present(handle_top_bottom))) then
      k = mesh%half_kds
      ! ---?--- 1
      !
      ! ===o=== 1   x1
      !
      ! -------
      !
      ! ===o=== 2   x2
      !
      ! -------
      !
      ! ===o=== 3   x3
      x1 = mesh%full_lev(k  ) - mesh%half_lev(k)
      x2 = mesh%full_lev(k+1) - mesh%half_lev(k)
      x3 = mesh%full_lev(k+2) - mesh%half_lev(k)
      a =  x2 * x3 / (x1**2 - x1 * x2 - x1 * x3 + x2 * x3)
      b =  x1 * x3 / (x2**2 - x2 * x1 - x2 * x3 + x1 * x3)
      c =  x1 * x2 / (x3**2 - x3 * x1 - x3 * x2 + x1 * x2)
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%half_ids, mesh%half_ide
          x_lev_lon(:,i,j,k) = a * x_lon(:,i,j,k) + b * x_lon(:,i,j,k+1) + c * x_lon(:,i,j,k+2)
        end do
      end do
      k = mesh%half_kde
      ! ===o=== NLEV - 2  x3
      !
      ! -------
      !
      ! ===o=== NLEV - 1  x2
      !
      ! -------
      !
      ! ===o=== NLEV      x1
      !
      ! ---?--- NLEV + 1
      x1 = mesh%half_lev(k) - mesh%full_lev(k-1)
      x2 = mesh%half_lev(k) - mesh%full_lev(k-2)
      x3 = mesh%half_lev(k) - mesh%full_lev(k-3)
      a =  x2 * x3 / (x1**2 - x1 * x2 - x1 * x3 + x2 * x3)
      b =  x1 * x3 / (x2**2 - x2 * x1 - x2 * x3 + x1 * x3)
      c =  x1 * x2 / (x3**2 - x3 * x1 - x3 * x2 + x1 * x2)
      do j = mesh%full_jds, mesh%full_jde
        do i = mesh%half_ids, mesh%half_ide
          x_lev_lon(:,i,j,k) = a * x_lon(:,i,j,k-1) + b * x_lon(:,i,j,k-2) + c * x_lon(:,i,j,k-3)
        end do
      end do
    end if
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lon_edge_to_lev_lon_edge

  subroutine interp_lat_edge_to_lev_lat_edge(mesh, x_lat, x_lev_lat, handle_top_bottom)

    type(mesh_type), intent(in) :: mesh
    real(r8), intent(in) :: x_lat(member_num ,                       &
                                  mesh%full_ims:mesh%full_ime, &
                                  mesh%half_jms:mesh%half_jme, &
                                  mesh%full_kms:mesh%full_kme)
    real(r8), intent(out) :: x_lev_lat(member_num ,                       &
                                       mesh%full_ims:mesh%full_ime, &
                                       mesh%half_jms:mesh%half_jme, &
                                       mesh%half_kms:mesh%half_kme)
    logical, intent(in), optional :: handle_top_bottom

    integer i, j, k
    real(r8) x1, x2, x3, a, b, c

    ! -------
    !
    ! ===o=== k-1
    !
    ! ---?--- k
    !
    ! ===o=== k
    !
    ! -------
#ifdef Detail_Time
    call Get_Start_Time(cal_time_start)
    call Add_Function("interp_lat_edge_to_lev_lat_edge")
    call Indent_In()
#endif
    do k = mesh%half_kds + 1, mesh%half_kde - 1
      a = mesh%full_dlev(k-1) / (mesh%full_dlev(k-1) + mesh%full_dlev(k))
      b = mesh%full_dlev(k  ) / (mesh%full_dlev(k-1) + mesh%full_dlev(k))
      do j = mesh%half_jds, mesh%half_jde
        do i = mesh%full_ids, mesh%full_ide
          x_lev_lat(:,i,j,k) = a * x_lat(:,i,j,k) + b * x_lat(:,i,j,k-1)
        end do
      end do
    end do

    if (merge(handle_top_bottom, .false., present(handle_top_bottom))) then
      k = mesh%half_kds
      ! ---?--- 1
      !
      ! ===o=== 1   x1
      !
      ! -------
      !
      ! ===o=== 2   x2
      !
      ! -------
      !
      ! ===o=== 3   x3
      x1 = mesh%full_lev(k  ) - mesh%half_lev(k)
      x2 = mesh%full_lev(k+1) - mesh%half_lev(k)
      x3 = mesh%full_lev(k+2) - mesh%half_lev(k)
      a =  x2 * x3 / (x1**2 - x1 * x2 - x1 * x3 + x2 * x3)
      b =  x1 * x3 / (x2**2 - x2 * x1 - x2 * x3 + x1 * x3)
      c =  x1 * x2 / (x3**2 - x3 * x1 - x3 * x2 + x1 * x2)
      do j = mesh%half_jds, mesh%half_jde
        do i = mesh%full_ids, mesh%full_ide
          x_lev_lat(:,i,j,k) = a * x_lat(:,i,j,k) + b * x_lat(:,i,j,k+1) + c * x_lat(:,i,j,k+2)
        end do
      end do
      k = mesh%half_kde
      ! ===o=== NLEV - 2  x3
      !
      ! -------
      !
      ! ===o=== NLEV - 1  x2
      !
      ! -------
      !
      ! ===o=== NLEV      x1
      !
      ! ---?--- NLEV + 1
      x1 = mesh%half_lev(k) - mesh%full_lev(k-1)
      x2 = mesh%half_lev(k) - mesh%full_lev(k-2)
      x3 = mesh%half_lev(k) - mesh%full_lev(k-3)
      a =  x2 * x3 / (x1**2 - x1 * x2 - x1 * x3 + x2 * x3)
      b =  x1 * x3 / (x2**2 - x2 * x1 - x2 * x3 + x1 * x3)
      c =  x1 * x2 / (x3**2 - x3 * x1 - x3 * x2 + x1 * x2)
      do j = mesh%half_jds, mesh%half_jde
        do i = mesh%full_ids, mesh%full_ide
          x_lev_lat(:,i,j,k) = a * x_lat(:,i,j,k-1) + b * x_lat(:,i,j,k-2) + c * x_lat(:,i,j,k-3)
        end do
      end do
    end if
    
#ifdef Detail_Time
    call Get_End_Time(cal_time_end)
    cal_time = cal_time + cal_time_end - cal_time_start
    call Indent_Out()
#endif

  end subroutine interp_lat_edge_to_lev_lat_edge

end module interp_mod

